import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.8.22"

    id("org.jetbrains.kotlin.kapt") version "1.8.22"
    id("org.jetbrains.kotlin.plugin.allopen") version "1.8.22"
    id("io.micronaut.application") version "4.0.0-M4"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        mavenContent { snapshotsOnly() }
    }
    mavenCentral()
}

configurations {
    micronaut {
        extendsFrom compileClasspath
    }
}

dependencies {
    kapt("io.micronaut:micronaut-http-validation")
    kapt("io.micronaut.serde:micronaut-serde-processor")

    micronaut("io.micronaut:micronaut-jackson-databind")
    micronaut("io.micronaut.kotlin:micronaut-kotlin-runtime")
    micronaut("io.micronaut.serde:micronaut-serde-jackson")
    micronaut("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    micronaut("com.fasterxml.jackson.module:jackson-module-kotlin")

    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
}


processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("jetty")
    processing {
        incremental(true)
        annotations("io.github.nickacpt.minecraftscreenshotendpoint.*")
    }
}
